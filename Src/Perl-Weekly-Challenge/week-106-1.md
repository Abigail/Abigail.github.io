# Perl Weekly Challenge 106: Maximum Gap

## [Challenge](https://perlweeklychallenge.org/blog/perl-weekly-challenge-106/#TASK1)

> You are given an array of integers `@N`.
>
> Write a script to display the maximum difference between two
> successive elements once the array is sorted.
>
> If the array contains only 1 element then display `0`.

### Examples
~~~~
Input: @N = (2, 9, 3, 5)
Output: 4

Input: @N = (1, 3, 8, 2, 0)
Output: 5

Input: @N = (5)
Output: 0
~~~~

## Solution

### Overview

The solution is pretty straight forward. After reading in the numbers,
we have to sort the list of number. Then we iterate over the
numbers, calculate the difference between the number and the
previous number, and keeping track of the maximum.

We will be reading input from standard input, where each line
contains a set of numbers, separated by white space. Each line
is seen as a different challenge.

### Perl

We are iterating line by line over the input (`<>`), and splitting
each line on whitespace ([`split`](#)). The resulting list is numerically
[`sort`](#)ed, and stored in an array (`@N`).

We're then finding all the differences between a number and
the preceeding number using `map`, starting from the second 
number in the array. We use the `max` function from the
module [`List::Util`](#) to get the maximum. If the input has just
one number, the list of differences is empty; it that case,
`max` returns `undef`. Hence the `// 0`, which means that
in that case, we print `0`.

~~~~
use List::Util qw [max];
while (<>) {
    my @N = sort {$a <=> $b} split;

    say max (map {$N [$_] - $N [$_ - 1]} 1 .. $#N) // 0;
}
~~~~

FULL_PROGRAM

### GNU AWK

For our AWK solution, we turn to GNU AWK, the GNU implemenation of AWK.
This implementation has the method [`asort`](#String-Functions), which
takes an array, and sorts it.

In AWK, the input is by default already split on whitespace, and
available in the variables `$1`, `$2`, `$3`, .... The variable
[`NF`](#index-NF-variable) contains the number of fields the input
has been split into. We can then iterate over the fields, and
populate an array `N`.

Finding the maximum of the differences is done by iterating over
the array.

~~~~
{   
    #
    # We don't have lexical variables, so we need to clear
    # the array N in each iteration.
    #
    delete N

    #
    # Read in the data, and populate N
    #
    for (i = 1; i <= NF; i ++) {
        N [i] = $i
    }

    #
    # Sort the array; for numeric values, this sorts numerically.
    #
    asort(N)

    #
    # Find the maximum of the differences. For a single element
    # array, the maximum will be 0.
    #
    max = 0
    for (i = 2; i <= NF; i ++) {
        if (N [i] - N [i - 1] > max) {
            max = N [i] - N [i - 1]
        }
    }

    #
    # Print the result.
    #
    print max
}
~~~~

FULL_PROGRAM
